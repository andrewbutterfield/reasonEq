{-
  LBNF Syntax for reasonEq Terms and Theories,
  with an emphasis on Unifying Theories of Programming

  We envisage the following sequence of increasingly complex syntaxes:
    Expressions
    Predicates
    Programs

  We are targetting the following Term data-structures:
   Value; Variable; VarSet; VarList; Type; Substn; ListVar; Term
  Ultimately both Expressions and Predicates will become Terms.
-}

-- we want to add a boolean type
token Boolean ( {"false"} | {"true"} ) ;

{- 
   We define a predicate type (expressions of type Bool)

   Propositional precedence:  tight..loose: ~ /\ \/ ==> ===
-}
PEqv.  Pred  ::= Pred "===" Pred1   ;
PImpl. Pred1 ::= Pred2 "==>" Pred1  ;
POr.   Pred2 ::= Pred2 "\\/" Pred3  ;
PAnd.  Pred3 ::= Pred3 "/\\" Pred4  ;
PNot.  Pred4 ::= "~" Pred5          ;
EQ.    Pred5 ::= Exp "==" Exp       ; 
NE.    Pred5 ::= Exp "!=" Exp       ; 
LT.    Pred5 ::= Exp "<" Exp        ; 
LE.    Pred5 ::= Exp "<=" Exp       ; 
GT.    Pred5 ::= Exp ">" Exp        ; 
GE.    Pred5 ::= Exp ">=" Exp       ; 
PVar.  Pred6 ::= Ident              ;
PLift. Pred6 ::= "E" Exp            ;

coercions Pred 6                    ;


{- 
    We then define a generic expression type.

    Haskell operators:
   Prc | Left-Assoc           |         Non-Assoc            | Right-Assoc
    9  | !!                   |                              | . 
    8  |                      |                              | ^ ^^ **
    7  | * / div mod rem quot |                              | 
    6  | + -                  |                              |
    5  |                      |                              | : ++
    4  |                      | == /= < <= > >= elem notElem | 
    3  |                      |                              | &&
    2  |                      |                              | ||
    1  | >> >>=               |                              |
    0  |                      |                              | $ $! seq
    
-}

ECat.  Exp  ::= Exp1  "++" Exp  ;
ECons. Exp  ::= Exp1  ":"  Exp  ;
EAdd.  Exp1 ::= Exp1  "+"  Exp2 ;
ESub.  Exp1 ::= Exp1  "-"  Exp2 ;
EMul.  Exp2 ::= Exp2  "*"  Exp3 ;
EDiv.  Exp2 ::= Exp2 "div" Exp3 ;
EMod.  Exp2 ::= Exp2 "mod" Exp3 ;
ENeg.  Exp2 ::= "neg" Exp3      ;
EInt.  Exp3 ::= Integer         ;
EVar.  Exp3 ::= Ident           ;
EBool. Exp3 ::= Boolean         ;
ENil.  Exp3 ::= "nil"           ;


coercions Exp 3               ;

